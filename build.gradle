plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group ='io.github.rb958'
archivesBaseName = "rkernel"
version = '1.0.1'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: username, password: password)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: username, password: password)
            }

            pom.project{
                name 'RBKernel'
                packaging 'jar'
                description 'It is a small kernel on which several modules can be grafted in the form of plugins. The kernel communicates with all the modules grafted on it and the modules can communicate with the other modules grafted on the kernel through signals that they emit to the kernel. With this kernel, there is no need to restart the running application just to add new features because the kernel detects and loads any new module.'
                url 'https://github.com/Rb958/rkernel'

                scm{
                    connection 'scm:github:https://github.com/Rb958/rkernel.git'
                    developerConnection 'https://github.com/Rb958/rkernel.git'
                    url 'https://github.com/Rb958/rkernel.git'
                }

                licenses{
                    license{
                        name 'The apache licence version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers{
                    developer{
                        id 'richie'
                        name 'Richie AKAWA'
                        email 'richiebayless@gmail.com'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'jakarta.activation:jakarta.activation-api:2.0.1'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'
    implementation 'com.sun.xml.bind:jaxb-core:3.0.2'
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}